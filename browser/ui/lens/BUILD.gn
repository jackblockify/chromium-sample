# Copyright 2024 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/chromeos/ui_mode.gni")

# Contained Lens dependencies are currently for desktop platforms only
assert(!is_android, "Lens Overlay is for desktop platforms only")

# TODO(crbug.com/339497734): Remove circular dependencies and move all sources
# to "lens_internal", at which point "lens_internal" can be renamed "lens".
source_set("lens") {
  sources = [
    "lens_overlay_controller.cc",
    "lens_overlay_controller.h",
    "lens_overlay_side_panel_coordinator.cc",
    "lens_overlay_side_panel_coordinator.h",
    "lens_overlay_side_panel_navigation_throttle.cc",
    "lens_overlay_side_panel_navigation_throttle.h",
    "lens_overlay_side_panel_web_view.cc",
    "lens_overlay_side_panel_web_view.h",
    "lens_overlay_theme_utils.cc",
    "lens_overlay_theme_utils.h",
    "lens_search_bubble_controller.cc",
    "lens_search_bubble_controller.h",
    "lens_untrusted_ui.cc",
    "lens_untrusted_ui.h",
    "lens_untrusted_ui_config.cc",
    "lens_untrusted_ui_config.h",
    "search_bubble_ui.cc",
    "search_bubble_ui.h",
  ]

  # TODO(crbug.com/339497734): It doesn't make sense to try to set up
  # public_deps while circular dependencies are allowed.
  public_deps = [
    ":lens_internal",
    "//chrome/browser/ui/views/side_panel",
  ]
  deps = [
    "//base",
    "//chrome/app:generated_resources_grit",
    "//chrome/browser/lens/core/mojom:mojo_bindings",
    "//chrome/browser/profiles:profile",
    "//chrome/browser/resources/lens/overlay:resources",
    "//chrome/browser/resources/lens/overlay:resources_grit",
    "//chrome/browser/resources/lens/overlay/search_bubble:resources",
    "//chrome/browser/resources/lens/overlay/search_bubble:resources_grit",
    "//chrome/browser/themes",
    "//chrome/browser/ui:browser_element_identifiers",
    "//chrome/browser/ui/browser_window",
    "//chrome/browser/ui/tabs",
    "//chrome/browser/ui/webui",
    "//chrome/browser/ui/webui/top_chrome",
    "//chrome/common",
    "//chrome/common:constants",
    "//components/google/core/common",
    "//components/input",
    "//components/lens:features",
    "//components/lens/proto/server:proto",
    "//components/omnibox/browser",
    "//components/permissions",
    "//components/sessions:session_id",
    "//components/signin/public/identity_manager",
    "//components/strings:components_strings_grit",
    "//components/vector_icons",
    "//components/viz/common",
    "//components/zoom",
    "//content/public/browser",
    "//content/public/common",
    "//mojo/public/cpp/bindings",
    "//net",
    "//third_party/lens_server_proto:lens_overlay_proto",
    "//ui/base",
    "//ui/compositor",
    "//ui/gfx/geometry",
    "//ui/native_theme",
    "//ui/views",
    "//ui/views/controls/webview",
    "//ui/webui",
    "//ui/webui/resources/cr_components/color_change_listener:mojom",
    "//ui/webui/resources/cr_components/searchbox:mojo_bindings",
  ]
  if (is_chromeos_ash) {
    deps += [
      "//ui/aura",
      "//ui/wm",
    ]
  }
}

source_set("lens_internal") {
  # The public interface is intentionally minimal. There should be a small
  # number of entry points and publicly used headers.
  public = [
    "lens_overlay_invocation_source.h",
    "lens_overlay_permission_utils.h",
  ]
  friend = [ ":*" ]
  sources = [
    "lens_overlay_colors.h",
    "lens_overlay_dismissal_source.h",
    "lens_overlay_image_helper.cc",
    "lens_overlay_image_helper.h",
    "lens_overlay_permission_utils.cc",
    "lens_overlay_proto_converter.cc",
    "lens_overlay_proto_converter.h",
    "lens_overlay_query_controller.cc",
    "lens_overlay_query_controller.h",
    "lens_overlay_request_id_generator.cc",
    "lens_overlay_request_id_generator.h",
    "lens_overlay_url_builder.cc",
    "lens_overlay_url_builder.h",
    "lens_permission_bubble_controller.cc",
    "lens_permission_bubble_controller.h",
    "search_bubble_page_handler.cc",
    "search_bubble_page_handler.h",
  ]

  deps = [
    "//base",
    "//chrome/app:generated_resources_grit",
    "//chrome/app/theme:theme_resources_grit",
    "//chrome/browser:browser_process",
    "//chrome/browser/lens/core/mojom:mojo_bindings",
    "//chrome/browser/profiles",
    "//chrome/browser/themes",
    "//chrome/browser/ui:browser_element_identifiers",
    "//chrome/browser/ui/browser_window",
    "//chrome/browser/ui/color:color_headers",
    "//chrome/browser/ui/webui/top_chrome",
    "//chrome/common:channel_info",
    "//components/constrained_window",
    "//components/endpoint_fetcher",
    "//components/language/core/common",
    "//components/lens:features",
    "//components/lens/proto/server:proto",
    "//components/prefs",
    "//components/signin/public/identity_manager",
    "//components/sync/service",
    "//components/sync_preferences",
    "//components/unified_consent",
    "//components/variations",
    "//components/variations:variations_mojom",
    "//components/version_info:channel",
    "//content/public/browser",
    "//google_apis",
    "//google_apis/common:common",
    "//mojo/public/cpp/bindings",
    "//net",
    "//net/traffic_annotation",
    "//services/network/public/cpp",
    "//skia",
    "//third_party/lens_server_proto:lens_overlay_proto",
    "//third_party/omnibox_proto",
    "//ui/base:types",
    "//ui/gfx:gfx",
    "//ui/gfx/codec:codec",
    "//ui/gfx/geometry:geometry",
    "//ui/views",
    "//url",
  ]
}

source_set("browser_tests") {
  testonly = true
  defines = [ "HAS_OUT_OF_PROC_TEST_RUNNER" ]
  sources = [ "lens_overlay_controller_browsertest.cc" ]
  deps = [
    ":lens",
    "//base",
    "//base/test:test_support",
    "//chrome/browser",
    "//chrome/browser/lens/core/mojom:mojo_bindings",
    "//chrome/browser/profiles:profile",
    "//chrome/browser/sync",
    "//chrome/browser/themes",
    "//chrome/browser/ui",
    "//chrome/browser/ui:browser_element_identifiers",
    "//chrome/browser/ui/tabs",
    "//chrome/browser/ui/views/side_panel",
    "//chrome/test:test_support_ui",
    "//components/lens:features",
    "//components/lens/proto/server:proto",
    "//components/permissions:test_support",
    "//components/prefs",
    "//content/public/browser",
    "//content/public/common",
    "//content/test:test_support",
    "//mojo/public/cpp/bindings",
    "//net",
    "//ui/base",
    "//ui/base:types",
    "//ui/compositor",
    "//ui/events:events_base",
    "//ui/views",
    "//ui/views:test_support",
    "//ui/views/controls/webview",
    "//url",
  ]
}

source_set("interactive_ui_tests") {
  testonly = true
  defines = [ "HAS_OUT_OF_PROC_TEST_RUNNER" ]
  sources = [
    "lens_overlay_controller_interactive_uitest.cc",
    "lens_permission_bubble_controller_interactive_uitest.cc",
  ]
  deps = [
    ":lens",
    "//base/test:test_support",
    "//chrome/browser",
    "//chrome/browser/profiles:profile",
    "//chrome/browser/search_engines",
    "//chrome/browser/ui",
    "//chrome/browser/ui:browser_element_identifiers",
    "//chrome/browser/ui/tabs",
    "//chrome/test:test_support",
    "//chrome/test:test_support_ui",
    "//components/feature_engagement/public",
    "//components/lens:features",
    "//components/prefs",
    "//components/search_engines",
    "//components/user_education/views",
    "//content/test:test_support",
    "//testing/gtest",
  ]
}

source_set("unit_tests") {
  testonly = true
  sources = [
    "lens_overlay_image_helper_unittest.cc",
    "lens_overlay_proto_converter_unittest.cc",
    "lens_overlay_query_controller_unittest.cc",
    "lens_overlay_request_id_generator_unittest.cc",
    "lens_overlay_side_panel_navigation_throttle_unittest.cc",
    "lens_overlay_url_builder_unittest.cc",
  ]
  deps = [
    ":lens",
    "//base",
    "//base/test:test_support",
    "//chrome/browser",
    "//chrome/browser:browser_process",
    "//chrome/browser/lens/core/mojom:mojo_bindings",
    "//chrome/browser/themes",
    "//chrome/test:test_support",
    "//components/endpoint_fetcher",
    "//components/lens:features",
    "//content/public/browser",
    "//content/test:test_support",
    "//google_apis/common",
    "//net",
    "//net/traffic_annotation",
    "//testing/gtest",
    "//third_party/icu:icui18n",
    "//third_party/icu:icuuc_public",
    "//third_party/lens_server_proto:lens_overlay_proto",
    "//third_party/omnibox_proto",
    "//ui/gfx",
    "//ui/gfx/codec",
    "//ui/gfx/geometry",
    "//url",
  ]
}
